form      = _{ SOI ~ header ~ (block | emptyline)* ~ EOI }
emptyline = _{ NEWLINE }
header    =  { "# " ~ question_text }
block     = _{
    text_input
  | textarea
  | checkbox
  | radio
//   | dropdown
  | submit
  | comment
}

textarea =  { ^"textarea:" ~ SPACE? ~ question_with_default? }
checkbox =  {
    "checkbox:" ~ SPACE? ~ question_text? ~ NEWLINE ~ listitem_check+
}
radio    =  {
    ^"radio:" ~ SPACE? ~ question_text ~ NEWLINE ~ listitem+
}
dropdown =  {
    ^"dropdown:" ~ SPACE? ~ question_text ~ NEWLINE ~ listitem+
}
submit   =  {
    ^"submit:" ~ question_with_default ~ NEWLINE?
}
comment  = _{ "//" ~ (!"\n" ~ ANY)* }

text_input            =  {
    ^"text:" ~ SPACE? ~ question_with_default ~ SPACE?
}
question_with_default = _{
    (default_value ~ SPACE? ~ question_text)
  | (question_text ~ SPACE? ~ default_value)
  | (SPACE? ~ question_text)
}
question_text         =  { (!NEWLINE ~ !" [" ~ ANY)+ }
default_value         = _{ "[" ~ inner_default_value ~ "]" }
inner_default_value   =  { (!"]" ~ !NEWLINE ~ ANY)+ }
listitem              =  { SPACE+? ~ "- " ~ SPACE? ~ question_text ~ NEWLINE? }
listitem_check        =  { SPACE+? ~ "- " ~ (checked | unchecked) ~ SPACE? ~ question_text ~ NEWLINE? }
checked               =  { "[x]" }
unchecked             =  { "[ ]" }

// Helpers
SPACE = _{ " " }
